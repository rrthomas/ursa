// Ursa grammar.
// Â© Reuben Thomas 2023
// Released under the MIT license.

Ursa {
  Sequence = ListOf<Exp, #sc> #sc

  // A semicolon is "automatically inserted" if a newline or the end of the input stream is
  // reached, or the next token is "}".
  // NOTE: Applications of this rule *must* appear in a lexical context -- either in the body of a
  // lexical rule, or inside `#()`.
  sc = space* (";" | end)
     | spacesNoNL (lineTerminator | &"}")

  // Optional final comma for comma-separated lists.
  maybeCommaAngle = space* ("," | &">")
  maybeCommaParen = space* ("," | &")")
  maybeCommaBracket = space* ("," | &"]")
  maybeCommaBrace = space* ("," | &"}")

  PrimaryExp
    = null              -- null
    | bool
    | string
    | literalString
    | number
    | Object
    | ident             -- ident
    | List
    | Map
    | Block
    | "(" Exp ")"       -- paren

  Definition = ident "=" Exp
  Initializer = "=" Exp // TODO: Use this in Definition

  List = "[" ListOf<AssignmentExp, ","> #maybeCommaBracket "]"

  Object = NamedType "{" ListOf<Definition, #sc> #sc "}"

  Map = "{" ListOf<KeyValue, ","> #maybeCommaBrace "}"
  KeyValue = Exp ":" Exp

  PropertyExp
    = PropertyExp "." ident             -- property
    | Fn
    | PrimaryExp

  CallExp
    = CallExp "." ident     -- property
    | CallExp Arguments     -- call
    | PropertyExp Arguments -- property_call
  Arguments = "(" ListOf<Exp, ","> #maybeCommaParen ")"

  LvalueExp
    = CallExp
    | PropertyExp

  Ifs = NonemptyListOf<If, else> (else Block)?
  If = if Exp Block

  Fn = FnType Block
  FnType = fn "(" ListOf<Param, ","> #maybeCommaParen ")" TypeAnnotation?
  Param = ident TypeAnnotation?

  Loop = loop Block

  For = for ident of Exp Block

  UnaryExp
    = "~" UnaryExp    -- bitwise_not
    | "+" UnaryExp    -- pos
    | "-" UnaryExp    -- neg
    | LvalueExp

  ExponentExp
    = UnaryExp "**" ExponentExp  -- power
    | UnaryExp

  ProductExp
    = ProductExp "*" ExponentExp  -- times
    | ProductExp "/" ExponentExp  -- divide
    | ProductExp "%" ExponentExp  -- mod
    | ExponentExp

  SumExp
    = SumExp "+" ProductExp  -- plus
    | SumExp "-" ProductExp  -- minus
    | ProductExp

  CompareExp
    = CompareExp "==" SumExp -- eq
    | CompareExp "!=" SumExp -- neq
    | CompareExp "<"  SumExp -- lt
    | CompareExp "<=" SumExp -- leq
    | CompareExp ">"  SumExp -- gt
    | CompareExp ">=" SumExp -- geq
    | SumExp

  BitwiseExp
    = BitwiseExp "&" CompareExp   -- and
    | BitwiseExp "^" CompareExp   -- xor
    | BitwiseExp "|" CompareExp   -- or
    | BitwiseExp "<<" CompareExp  -- lshift
    | BitwiseExp ">>" CompareExp  -- arshift
    | BitwiseExp ">>>" CompareExp -- lrshift
    | CompareExp

  LogicNotExp
    = not BitwiseExp          -- not
    | BitwiseExp

  LogicExp
    = LogicExp and LogicNotExp -- and
    | LogicExp or LogicNotExp  -- or
    | LogicNotExp

  AssignmentExp
    = LvalueExp ":=" AssignmentExp -- ass
    | LogicExp

  Exp
    = Ifs
    | Loop
    | For
    | AssignmentExp
    | Lets
    | Use
    | break Exp?      -- break
    | continue        -- continue
    | return Exp?     -- return

  Lets = NonemptyListOf<Let, and>
  Let = let Definition

  Use = use Path

  Block = "{" Sequence "}"

  // Types
  NamedType = Path TypeArgs?
  Type
    = NonemptyListOf<NamedType, "+"> -- intersection
    | FnType                         -- fn
  TypeParams = "<" ListOf<TypeParam, ","> #maybeCommaAngle ">"
  TypeParam = ident TypeAnnotation
  TypeArgs
    = "<" ListOf<Type, ","> #maybeCommaAngle ">"
  NamedTypeAnnotation = ":" NamedType
  TypeAnnotation = ":" Type
  FnTypeAnnotation = ":" FnType

  // Classes
  Class = class ident TypeParams NamedTypeAnnotation "{" ListOf<ClassMember, #sc> #sc "}"
  ClassMember = ClassField | ClassMethod
  ClassField = pub? static? var? ident TypeAnnotation? Initializer?
  ClassMethod = pub? static? ident TypeParams? "=" Fn

  // Traits
  Trait = trait ident TypeParams NamedTypeAnnotation "{" ListOf<TraitMember, #sc> #sc "}"
  TraitMember = TraitField | TraitMethod
  TraitField = var? ident TypeAnnotation?
  TraitMethod = ident TypeParams? FnTypeAnnotation

  // Convenience rules for parsing keyword tokens.
  and = "and" ~identifierPart
  break = "break" ~identifierPart
  class = "class" ~identifierPart
  continue = "continue" ~identifierPart
  else = "else" ~identifierPart
  fn = "fn" ~identifierPart
  for = "for" ~identifierPart
  if = "if" ~identifierPart
  let = "let" ~identifierPart
  loop = "loop" ~identifierPart
  not = "not" ~identifierPart
  of = "of" ~identifierPart
  or = "or" ~identifierPart
  pub = "pub" ~identifierPart
  return = "return" ~identifierPart
  static = "static" ~identifierPart
  trait = "trait" ~identifierPart
  use = "use" ~identifierPart
  var = "var" ~identifierPart

  keyword = and | break | class | continue | else | fn | for | if | let | loop
    | not | of | or | pub | return | static | trait | use | var

  reservedWord = bool | null | keyword

  // Make ident indirect so we can easily get a node with the whole identifier.
  ident = identName
  identName = ~reservedWord identifierStart identifierPart*
  identifierStart = letter | "_"
  identifierPart = identifierStart | digit

  Path = NonemptyListOf<ident, ".">

  null = "null" ~identifierPart

  bool = ("false" | "true") ~identifierPart

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole

  string = "\"" stringCharacter* "\""
  stringCharacter
    = ~("\"" | "\\" | lineTerminator) any     -- nonEscaped
    | "\\" escapeSequence    -- escaped
    | lineContinuation       -- lineContinuation
  lineContinuation = "\\" lineTerminatorSequence
  escapeSequence
    = unicodeEscapeSequence
    | hexEscapeSequence
    | characterEscapeSequence  // Must come last.
  characterEscapeSequence
    = singleEscapeCharacter
    | nonEscapeCharacter
  singleEscapeCharacter =
    "\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v"
  nonEscapeCharacter
    = ~(escapeCharacter | lineTerminator) any
  escapeCharacter
    = singleEscapeCharacter | digit | "x" | "u"
  hexEscapeSequence
    = "x" hexDigit hexDigit
  unicodeEscapeSequence
    = "u" hexDigit hexDigit hexDigit hexDigit
  lineTerminator
    = "\n" | "\r" | "\u2028" | "\u2029"
  lineTerminatorSequence
    = "\n" | "\r" ~"\n" | "\u2028" | "\u2029" | "\r\n"

  literalString
    = "r\"" (~"\"" any)* "\""
    | "r#\"" (~"\"#" any)* "\"#"
    | "r##\"" (~"\"##" any)* "\"##"
    | "r###\"" (~"\"###" any)* "\"###"
    | "r####\"" (~"\"####" any)* "\"####"

  comment = blockComment | lineComment
  blockComment = "/*" (~("*/" | "/*") any)* blockComment? (~"*/" any)* "*/"
  lineComment = "//" (~lineTerminator any)*

  // Override Ohm's built-in definition
  space := whitespace | lineTerminator | comment

  // Does not accept lineTerminators
  spacesNoNL = (whitespace | comment)*

  whitespace
    = "\t"
    | "\x0B"    -- verticalTab
    | "\x0C"    -- formFeed
    | " "
    | "\u00A0"  -- noBreakSpace
    | "\uFEFF"  -- byteOrderMark
    | unicodeSpaceSeparator

  unicodeSpaceSeparator = "\u2000".."\u200B" | "\u3000"
}
